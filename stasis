#!/usr/bin/perl -w

# Copyright (c) 2008, Gian Merlino
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
#    1. Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#    2. Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR IMPLIED
# WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO
# EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
# PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
# OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, 
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
# OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF 
# ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use strict;
use warnings;
use lib 'lib';
use open ':encoding(utf8)';
use Stasis::Parser;
use Stasis::LogSplit;
use Stasis::ClassGuess;
use Stasis::Extension;
use Stasis::ActorPage;
use Stasis::ChartPage;
use Stasis::PageMaker;
use File::Find qw();
use File::Path qw();
use File::Spec qw();
use POSIX;
use HTML::Entities;
use Getopt::Long;

## SUBS ##

sub usage() {
    my $prog = $0;
    $prog =~ s/^.*\///;
    print STDERR "Usage: $0 command [options]\n";
    print STDERR "See 'perldoc $prog'\n";
    exit 1;
}

sub bomb($) {
    my $msg = shift;
    my $prog = $0;
    $msg =~ s/^/$prog: /gm;
    chomp $msg;
    print STDERR $msg . "\n";
    exit 1;
}

sub status($) {
    my $msg = shift;
    my $prog = $0;
    
    if( $msg !~ /^[a-z]/ ) {
        $msg =~ s/^/$prog: /gm;
    }
    
    chomp $msg;
    if( $msg =~ /\.\.\s*$/ ) {
        print STDERR $msg;
    } else {
        print STDERR $msg . "\n";
    }
}

# For File::Find later
my @dataxmls;
sub wantedDataXML {
    if( -f $_ && $File::Find::name =~ /\/sws-[0-9]+\/data.xml$/ ) {
        push @dataxmls, $File::Find::name;
    }
}

## CODE ##

# Get the action first.
usage unless @ARGV;
my $action = lc shift @ARGV;

# Get options.
my $opt_version = 2;
my $opt_minlength = 30;
my $opt_dir = ".";
my $opt_nowrite;
my $opt_hintsfile;
my $opt_logger;
my $opt_attempts;

my $rc = GetOptions(
    "version=s"     => \$opt_version,
    "logger=s"      => \$opt_logger,
    #"hintsfile=s"   => \$opt_hintsfile,
    "dir=s"         => \$opt_dir,
    "minlength=i"   => \$opt_minlength,
    "attempts"      => \$opt_attempts,
    "nowrite"       => \$opt_nowrite,
	);

# Clean up opt_dir
if( $opt_dir ) {
    $opt_dir = File::Spec->rel2abs($opt_dir);
}

# Create a parser.
my $parser = Stasis::Parser->new( version => $opt_version, logger => $opt_logger );

# Set encoding of STDIN and STDOUT to UTF-8 (which Blizzard uses for combat logs)
binmode(STDIN, ":utf8");
binmode(STDOUT, ":utf8");

# Case out the various actions.
if( $action eq "print" ) {
    # action PRINT: used to print out actions from a log file
    while( <STDIN> ) {
        my %entry = $parser->parse($_);
        if( $entry{action} ) {
            my $text = $parser->toString( \%entry );
            print "$text\n" if $text;
        }
    }
} elsif( $action eq "check" ) {
    # action CHECK: see how many unrecognizable and unprintable actions there are.
    my $actions = 0;
    my $unrecognized = 0;
    my $unprinted = 0;
    
    while( <STDIN> ) {
        $actions ++;
        my %entry = $parser->parse($_);
        if( $entry{action} ) {
            my $text = $parser->toString( \%entry );
            
            if( !$text ) {
                $unprinted ++;
                #print STDERR "noprint: $_";
            }
        } else {
            print STDERR "noparse: $_";
            $unrecognized ++;
        }
    }
    
    print STDERR sprintf "%d total actions: %d (%0.1f%%) recognized, %d (%0.1f%%) printable\n", 
        $actions, 
        $actions - $unrecognized, 
        $actions ? floor( ($actions-$unrecognized) / $actions * 1000 )/10 : 0, 
        $actions - $unrecognized - $unprinted, 
        $actions ? floor( ($actions-$unrecognized-$unprinted) / $actions * 1000 )/10 : 0;
} elsif( $action eq "add" ) {
    # Going to output in $opt_dir
    bomb "not a directory: $opt_dir" unless $opt_dir && -d $opt_dir;
    status "Using directory: $opt_dir";
    
    # Check if $opt_hints (hints file) exists
    bomb "not a file: $opt_hintsfile" if( $opt_hintsfile && ! -f $opt_hintsfile && ! -l $opt_hintsfile );
    
    # Read the log file to @log
    status "Reading log file.";
    my @log;
    while( <STDIN> ) {
        my %entry = $parser->parse($_);
        push @log, \%entry if $entry{action};
    }
    
    # Done with STDIN.
    close STDIN;
    
    # Assign classes to %raid
    status "Assigning classes and pets .. ";
    my $classer = Stasis::ClassGuess->new( hintsfile => $opt_hintsfile );
    $classer->read_hints() if $opt_hintsfile;
    my %raid = $classer->guess(\@log);
    
    # Count them so the user can see what's up.
    my $n_players = 0;
    my $n_pets = 0;
    while( my ($rid, $rdata) = each(%raid) ) {
        if( $rdata->{class} ne "Pet" ) {
            $n_players ++;
        } else {
            $n_pets ++;
        }
    }
    
    status "found $n_players player" . ($n_players == 1 ? "" : "s") . " and $n_pets pet" . ($n_pets == 1 ? "" : "s") . ".";
    
    # Split the log to %splits
    status "Splitting out boss attempts .. ";
    my $splitter = Stasis::LogSplit->new;
    my %splits = $splitter->split(\@log);
    
    status "found " . scalar(keys %splits) . " boss encounter" . (scalar(keys %splits) == 1 ? "" : "s") . ".";
    
    # Compute statistics.
    my @ext_names = qw(Activity Damage Death Healing Aura Cast ExtraAttack Interrupt Power Presence Index);
    my %exts;
    foreach (@ext_names) {
        $exts{$_} = Stasis::Extension->factory($_);
    }
    
    while( my ($boss, $split) = each(%splits) ) {
        # Check minlength option.
        if( $opt_minlength && $split->{end} - $split->{start} < $opt_minlength ) {
            status sprintf "Skipping: %s (too short).", $boss, $split->{end} - $split->{start};
            next;
        }
        
        # Check attempts option.
        if( !$opt_attempts && !$split->{kill} ) {
            status sprintf "Skipping: %s (not a kill).", $boss, $split->{end} - $split->{start};
            next;
        }
        
        # Run statistics for this boss.
        my $dname = sprintf "%s/sws-%d", $opt_dir, floor($split->{start});
        status sprintf "Processing: %s (sws-%d).", $boss, floor($split->{start});
        
        # Start all the extensions.
        foreach my $ext (values %exts) {
            $ext->start();
        }
        
        # Process each line.
        foreach my $entry (@log[$split->{startLine} .. $split->{endLine}]) {
            foreach my $ext (values %exts) {
                $ext->process($entry);
            }
        }
        
        # End all the extensions.
        foreach my $ext (values %exts) {
            $ext->finish();
        }
        
        # Write the files.
        unless( $opt_nowrite ) {
            eval {
                # Remove the directory if it exists.
                File::Path::rmtree($dname) if -d $dname;

                # Create the directory.
                mkdir $dname or die $!;

                # Write the index.
                my $charter = Stasis::ChartPage->new( name => $boss, raid => \%raid, ext => \%exts );
                my ($chart_xml, $chart_html) = $charter->page;
                open CHARTPAGE, ">$dname/index.html" or die;
                print CHARTPAGE $chart_html;
                close CHARTPAGE;

                # Write the actor files.
                my $ap = Stasis::ActorPage->new( name => $boss, raid => \%raid, ext => \%exts );
                foreach my $actor (keys %{$exts{Presence}{actors}}) {
                    my $id = lc $actor;
                    $id = Stasis::PageMaker->tameText($id);

                    open ACTORPAGE, sprintf ">$dname/actor_%s.html", $id or die;
                    print ACTORPAGE $ap->page($actor);
                    close ACTORPAGE;
                }

                # Write the data.xml file.
                open DATAXML, ">$dname/data.xml" or die;
                print DATAXML $chart_xml;
                close DATAXML;
            }; if( $@ ) {
                bomb $@;
            }
        }
    }
} elsif( $action eq "rm" ) {
    # Going to output in $opt_dir
    bomb "not a directory: $opt_dir" unless $opt_dir && -d $opt_dir;
    status "Using directory: $opt_dir";
    
    # Read the log file to @log
    status "Reading log file.";
    my @log;
    while( <STDIN> ) {
        my %entry = $parser->parse($_);
        push @log, \%entry if $entry{action};
    }
    
    # Done with STDIN.
    close STDIN;
    
    # Split the log to %splits
    status "Splitting out boss attempts.";
    my $splitter = Stasis::LogSplit->new;
    my %splits = $splitter->split(\@log);
    
    while( my ($boss, $split) = each(%splits) ) {
        # Remove the directories.
        unless( $opt_nowrite ) {
            eval {
                # Remove the directory if it exists.
                my $dname = sprintf "%s/sws-%d", $opt_dir, floor($split->{start});
                
                if( -d $dname ) {
                    status sprintf "Removing: %s (sws-%d).", $boss, floor($split->{start});
                    File::Path::rmtree($dname);
                }
            }; if( $@ ) {
                bomb $@;
            }
        }
    }
} elsif( $action eq "history" ) {
    # Going to output in $opt_dir
    bomb "not a directory: $opt_dir" unless $opt_dir && -d $opt_dir;
    status "Using directory: $opt_dir";
    
    # Header
    my $xml = "<wws-history>\n";
    
    # Look at all data.xmls.
    @dataxmls = ();
    File::Find::find( \&wantedDataXML, ($opt_dir) );
    
    status "Found " . (scalar @dataxmls) . " subdirectories.";
    
    foreach my $dataxml (@dataxmls) {
        open DXML, $dataxml or die "Could not open a subdirectory for reading.";
        while( <DXML> ) {
            $xml .= $_;
        }
        close DXML;
    }
    
    # Footer
    $xml .= "</wws-history>\n";
    
    # Create wws-history directory if it doesn't exist.
    if( ! -d $opt_dir . "/wws-history" ) {
        mkdir $opt_dir . "/wws-history";
    }
    
    open DXML, ">$opt_dir/wws-history/data.xml" or die "Could not open data.xml for writing";
    print DXML $xml;
    close DXML;
    
    status "Wrote: $opt_dir/wws-history/data.xml";
} else {
    bomb "bad action: $action";
}

exit 0;
